// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Not(in=instruction[15], out=isA);
    Not(in=isA, out=isC);

    /* Handle A register. */
    Mux16(a=instruction, b=aluOut, sel=instruction[15], out=aRegIn);
    And(a=isC, b=instruction[5], out=cLoad);
    Or(a=isA, b=cLoad, out=aRegLoad);
    ARegister(in=aRegIn, load=aRegLoad, out=aRegOut, out[0..14]=addressM);

    /* Handle D register. */
    And(a=instruction[4], b=isC, out=loadD);
    DRegister(in=aluOut, load=loadD, out=dRegOut);

    Mux16(a=aRegOut, b=inM, sel=instruction[12], out=aluMem);

    ALU(
        x=dRegOut, 
        y=aluMem, 
        zx=instruction[11], 
        nx=instruction[10], 
        zy=instruction[9], 
        ny=instruction[8], 
        f=instruction[7], 
        no=instruction[6], 
        out=outM, 
        out=aluOut,
        zr=zr, 
        ng=ng
    );

    /* Set writeM. */
    And(a=isC, b=instruction[3], out=writeM);
    
    /* Set nl. */
    Not(in=ng, out=notng);
    Not(in=zr, out=notzr);
    And(a=notng, b=notzr, out=nl);

    /* Handle jumps. */
    Not(in=instruction[2], out=j1Illegal);
    And(a=ng, b=j1Illegal, out=j1Breach);

    Not(in=instruction[1], out=j2Illegal);
    And(a=zr, b=j2Illegal, out=j2Breach);

    Not(in=instruction[0], out=j3Illegal);
    And(a=nl, b=j3Illegal, out=j3Breach);

    Or(a=j1Breach, b=j2Breach, out=j12Breach);
    Or(a=j12Breach, b=j3Breach, out=breach);
    Not(in=breach, out=nobreach);

    And(a=nobreach, b=isC, out=jump);

    /* Handle PC.*/
    PC(in=aRegOut, load=jump, inc=true, reset=reset, out[0..14]=pc);
}
